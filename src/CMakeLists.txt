cmake_minimum_required(VERSION 3.18)

set(PROJECT_NAME cuda_bundle_adjustment)
set(INTERFACE_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../include)
set(INTERNAL_INCLUDE_DIR ${INTERFACE_INCLUDE_DIR})

# create project
project(${PROJECT_NAME} LANGUAGES CXX CUDA)

# dependent packages
find_package(Eigen3 REQUIRED)
find_package(CUDAToolkit REQUIRED)

# target configuration
file(GLOB SRCS ./*.cpp ./*.cu ./*.h* ${INTERNAL_INCLUDE_DIR}/*.h*)
add_library(${PROJECT_NAME})
target_sources(${PROJECT_NAME} PRIVATE ${SRCS})
target_include_directories(${PROJECT_NAME} PRIVATE ${INTERNAL_INCLUDE_DIR} ${EIGEN3_INCLUDE_DIR} ${CUDAToolkit_INCLUDE_DIRS})
target_compile_features(${PROJECT_NAME} PRIVATE cxx_std_17)
target_link_libraries(${PROJECT_NAME} PUBLIC CUDA::cudart CUDA::cusparse CUDA::cusolver)
set_target_properties(${PROJECT_NAME} PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${INTERFACE_INCLUDE_DIR})
set_target_properties(${PROJECT_NAME} PROPERTIES CUDA_ARCHITECTURES ${CUDA_ARCHS})

target_compile_options(${PROJECT_NAME} PRIVATE
$<$<COMPILE_LANG_AND_ID:CXX,GNU>:-Wall -O3>
$<$<COMPILE_LANG_AND_ID:CXX,Clang>:-Wall -O3>
$<$<COMPILE_LANG_AND_ID:CXX,MSVC>:/wd4819>
$<$<COMPILE_LANGUAGE:CUDA>:-lineinfo>
$<$<AND:$<COMPILE_LANGUAGE:CUDA>,$<CXX_COMPILER_ID:MSVC>>:-lineinfo -Xcompiler="\"/wd4819 /wd4244\"">
)

target_compile_definitions(${PROJECT_NAME} PRIVATE
$<$<COMPILE_LANG_AND_ID:CXX,MSVC>:_SILENCE_CXX17_NEGATORS_DEPRECATION_WARNING>
$<$<BOOL:${USE_FLOAT32}>:USE_FLOAT32>
)

install(
	TARGETS ${PROJECT_NAME}
	ARCHIVE DESTINATION ${CMAKE_INSTALL_PREFIX}/lib
	LIBRARY DESTINATION ${CMAKE_INSTALL_PREFIX}/lib
	RUNTIME DESTINATION ${CMAKE_INSTALL_PREFIX}/bin
)

install(
	DIRECTORY ${INTERFACE_INCLUDE_DIR}
	DESTINATION ${CMAKE_INSTALL_PREFIX}
	FILES_MATCHING PATTERN "*.h" PATTERN "*.hpp"
)
